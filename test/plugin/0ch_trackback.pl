#============================================================================================================
#
#	拡張機能 - トラックバック補助機能
#	0ch_trackback.pl
#	---------------------------------------------------------------------------
#	2005.11.11 start
#	2005.12.30 2ch互換仕様に修正
#
#============================================================================================================
package ZPL_trackback;

#------------------------------------------------------------------------------------------------------------
#
#	コンストラクタ
#	-------------------------------------------------------------------------------------
#	@param	なし
#	@return	オブジェクト
#
#------------------------------------------------------------------------------------------------------------
sub new
{
	my	$this = shift;
	my	$obj;
	
	$obj = {
		'LOGGER' => undef
	};
	bless($obj,$this);
	return $obj;
}

#------------------------------------------------------------------------------------------------------------
#
#	拡張機能名称取得
#	-------------------------------------------------------------------------------------
#	@param	なし
#	@return	名称文字列
#
#------------------------------------------------------------------------------------------------------------
sub getName
{
	my	$this = shift;
	return 'トラックバック補助機能\ ';
}

#------------------------------------------------------------------------------------------------------------
#
#	拡張機能説明取得
#	-------------------------------------------------------------------------------------
#	@param	なし
#	@return	説明文字列
#
#------------------------------------------------------------------------------------------------------------
sub getExplanation
{
	my	$this = shift;
	return 'トラックバックのための機能\を提供します。';
}

#------------------------------------------------------------------------------------------------------------
#
#	拡張機能タイプ取得
#	-------------------------------------------------------------------------------------
#	@param	なし
#	@return	拡張機能タイプ(スレ立て:1,レス:2,read:4,index:8,レス・スレ後:16)
#
#------------------------------------------------------------------------------------------------------------
sub getType
{
	my	$this = shift;
	return (4 | 8 | 16);
}

#------------------------------------------------------------------------------------------------------------
#
#	拡張機能実行インタフェイス
#	-------------------------------------------------------------------------------------
#	@param	$sys	MELKOR
#	@param	$form	SAMWISE
#	@param	$mode	呼び出しモード
#	@return	正常終了の場合は0
#
#------------------------------------------------------------------------------------------------------------
sub execute
{
	my	$this = shift;
	my	($sys,$form,$mode) = @_;
	
	# スレッド作成後またはレス書き込み後
	if(($mode == 16) && ($sys->Get('_ERR_') == 0)){
		doTrackBack($sys,$form);
	}
	# read.cgi,またはindex.html作成
	elsif(($mode == 4) || ($mode == 8)){
		addTrackBackURL($sys);
	}
	
	return 0;
}

#------------------------------------------------------------------------------------------------------------
#
#	トラックバック送信を行う
#	-------------------------------------------------------------------------------------
#	@param	$sys	システム情報
#	@param	$form	フォーム情報
#	@return	なし
#
#------------------------------------------------------------------------------------------------------------
sub doTrackBack
{
	my	($sys,$form) = @_;
	my	($contents,$pattern,$url);
	
	$contents = $form->Get('MESSAGE');
	if($contents =~ /<br>/){
		$pattern = "トラックバック:(http://.*?/.*?)<br>";
	}else{
		$pattern = "トラックバック:(http://.*?/.*)";
	}
	
	if($contents =~ /$pattern/){
		$url = $1;
		if(sendTrackBack($sys,$form,$url,\$contents) != 0){
			$contents .= '<br><font color=red>※トラックバック送信に失敗しました。（´・ω・`）</font>';
			$form->Set('MESSAGE',$contents);
		}
	}
}

#------------------------------------------------------------------------------------------------------------
#
#	トラックバックの送信
#	-------------------------------------------------------------------------------------
#	@param	$form	システム情報
#	@param	$url	ログ
#	@return	成功:0,失敗:-1
#
#------------------------------------------------------------------------------------------------------------
sub sendTrackBack
{
	my	($sys,$form,$url,$contents) = @_;
	my	($service,$tbURL,$tbTitle,$tbBlog,$tbContent,$ret,$oThread);
	
	$ret = -1;
	# トラックバック元URLの作成
	$tbURL .= $sys->Get('SERVER') . $sys->Get('CGIPATH') . '/read.cgi/';
	$tbURL .= $sys->Get('BBS') . '/' . $sys->Get('KEY') . '/l50';
	
	# トラックバックタイトルの作成
	$oThread = $sys->Get('_THREAD_');
	$tbTitle = $oThread->Get('SUBJECT',$sys->Get('KEY'));
	
	# トラックバック概要の作成
	if($sys->Get('_NUM_') == 1){
		$tbContent = $$contents;
	}else{
		require('./module/gondor.pl');
		my $oDat = new ARAGORN;
		my $path = $sys->Get('BBSPATH') . '/' . $sys->Get('BBS') . '/dat/' . $sys->Get('KEY') . '.dat';
		if($oDat->Load($sys,$path,1)){
			eval{
				my $pRes = $oDat->Get(0);
				my @elem = split(/<>/,$$pRes);
				$tbContent = $elem[3];
			};
			$oDat->Close();
		}
		undef($oDat);
	}
	$tbContent =~ s/<br>/\n/g;
	
	# 発ブログの作成
	$tbBlog = $sys->Get('_SET_')->Get('BBS_TITLE');
	
	# httpリクエスト作成
	require('./module/httpservice.pl');
	$service = new HTTPSERVICE;
	$service->init();
	$service->setMethod('POST');
	$service->setURI($url);
	$service->setParameter('url',$tbURL);
	$service->setParameter('title',$tbTitle);
	$service->setParameter('blog_name',$tbBlog);
	$service->setParameter('excerpt',$tbContent);
	
	# httpリクエスト送信
	if($service->request() == 1){
		my $response = $service->getResponse();
		if($response =~ /<error>0<\/error>/){
			$ret = 0;
		}
	}
	return $ret;
}

#------------------------------------------------------------------------------------------------------------
#
#	トラックバックURLの表示
#	-------------------------------------------------------------------------------------
#	@param	$sys	システム情報
#	@param	$logger	ログ
#	@return	なし
#
#------------------------------------------------------------------------------------------------------------
sub addTrackBackURL
{
	my	($sys) = @_;
	my	($contents,$tburl);
	
	# トラックバックURL情報の付加
	$tburl .= $sys->Get('SERVER') . $sys->Get('CGIPATH') . '/tb.cgi/';
	$tburl .= $sys->Get('BBS') . '/' . $sys->Get('KEY') . '/' . $sys->Get('_NUM_');
	$contents = $sys->Get('_DAT_');
	$contents->[2] .= ' <small><a href="' . $tburl . '">URL</a></small>';
}

#============================================================================================================
#	Module END
#============================================================================================================
1;
