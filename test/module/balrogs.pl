#============================================================================================================
#
#	検索モジュール(BALROGS)
#	balrogs.pl
#	-------------------------------------------------------------------------------------
#	2003.11.22 start
#	2004.09.18 システム改変に伴う変更
#
#============================================================================================================
package	BALROGS;

use strict;
use warnings;

#------------------------------------------------------------------------------------------------------------
#
#	コンストラクタ
#	-------------------------------------------------------------------------------------
#	@param	なし
#	@return	モジュールオブジェクト
#
#------------------------------------------------------------------------------------------------------------
sub new
{
	my $this = shift;
	
	my $obj = {
		'SYS'		=> undef,
		'TYPE'		=> undef,
		'SEARCHSET'	=> undef,
		'RESULTSET'	=> undef,
	};
	bless $obj, $this;
	
	return $obj;
}

#------------------------------------------------------------------------------------------------------------
#
#	検索設定
#	-------------------------------------------------------------------------------------
#	@param	$SYS    : MELKOR
#	@param	$mode   : 0:全検索,1:BBS内検索,2:スレッド内検索
#	@param	$type   : 0:全検索,1:名前検索,2:本文検索
#			          4:ID(日付)検索
#	@param	$bbs    : 検索BBS名($mode=1の場合に指定)
#	@param	$thread : 検索スレッド名($mode=2の場合に指定)
#	@return	なし
#
#------------------------------------------------------------------------------------------------------------
sub Create
{
	my $this = shift;
	my ($SYS, $mode, $type, $bbs, $thread) = @_;
	
	$this->{'SYS'} = $SYS;
	$this->{'TYPE'} = $type;
	
	$this->{'SEARCHSET'} = [];
	$this->{'RESULTSET'} = [];
	my $pSearchSet = $this->{'SEARCHSET'};
	
	# 鯖内全検索
	if ($mode == 0) {
		require './module/baggins.pl';
		require './module/nazguls.pl';
		my $BBSs = NAZGUL->new;
		
		$BBSs->Load($SYS);
		$BBSs->GetKeySet('ALL', '', ($_ = []));
		
		my $BBSpath = $SYS->Get('BBSPATH');
		
		foreach my $bbsID (@$_) {
			my $dir = $BBSs->Get('DIR', $bbsID);
			
			# 板ディレクトリに.0ch_hiddenというファイルがあれば読み飛ばす
			next if ( -e "$BBSpath/$dir/.0ch_hidden" );
			
			$SYS->Set('BBS', $dir);
			my $Threads = BILBO->new;
			$Threads->Load($SYS);
			$Threads->GetKeySet('ALL', '', ($_ = []));
			
			foreach my $threadID (@$_) {
				my $set = "$dir<>$threadID";
				push @$pSearchSet, $set;
			}
		}
	}
	# 掲示板内全検索
	elsif ($mode == 1) {
		require './module/baggins.pl';
		my $Threads = BILBO->new;
		
		$SYS->Set('BBS', $bbs);
		$Threads->Load($SYS);
		$Threads->GetKeySet('ALL', '', ($_ = []));
		
		foreach my $threadID (@$_) {
			my $set = "$bbs<>$threadID";
			push @$pSearchSet, $set;
		}
	}
	# スレッド内全検索
	elsif ($mode == 2) {
		my $set = "$bbs<>$thread";
		push @$pSearchSet, $set;
	}
	# 指定がおかすぃ
	else {
		return;
	}
	
	# datモジュール読み込み
	if (! defined $this->{'ARAGORN'}) {
		require './module/gondor.pl';
		$this->{'ARAGORN'} = ARAGORN->new;
	}
}

#------------------------------------------------------------------------------------------------------------
#
#	検索実行
#	-------------------------------------------------------------------------------------
#	@param	$word : 検索ワード
#	@param	$f    : 前結果クリアフラグ
#	@return	なし
#
#------------------------------------------------------------------------------------------------------------
sub Run
{
	my $this = shift;
	my ($word, $f) = @_;
	
	my $pSearchSet = $this->{'SEARCHSET'};
	$this->{'RESULTSET'} = [] if ($f);
	
	foreach (@$pSearchSet) {
		my ($bbs, $key) = split(/<>/, $_);
		$this->{'SYS'}->Set('BBS', $bbs);
		$this->{'SYS'}->Set('KEY', $key);
		Search($this, $word);
	}
	return $this->{'RESULTSET'};
}

#------------------------------------------------------------------------------------------------------------
#
#	検索結果取得
#	-------------------------------------------------------------------------------------
#	@param	なし
#	@return	結果セット
#
#------------------------------------------------------------------------------------------------------------
sub GetResultSet
{
	my $this = shift;
	
	return $this->{'RESULTSET'};
}

#------------------------------------------------------------------------------------------------------------
#
#	検索実装部
#	-------------------------------------------------------------------------------------
#	@param	$this : thisオブジェクト
#	@param	$word : 検索ワード
#	@return	なし
#
#------------------------------------------------------------------------------------------------------------
sub Search
{
	my ($this, $word) = @_;
	
	my $bbs = $this->{'SYS'}->Get('BBS');
	my $key = $this->{'SYS'}->Get('KEY');
	my $Path = $this->{'SYS'}->Get('BBSPATH') . "/$bbs/dat/$key.dat";
	my $ARAGORN = $this->{'ARAGORN'};
	
	if ($ARAGORN->Load($this->{'SYS'}, $Path, 1)) {
		my $pResultSet = $this->{'RESULTSET'};
		my $type = $this->{'TYPE'} || 0x7;
		
		# すべてのレス数でループ
		for (my $i = 0 ; $i < $ARAGORN->Size() ; $i++) {
			my $bFind = 0;
			my $pDat = $ARAGORN->Get($i);
			my @elem = split(/<>/, $$pDat, -1);
			
			# 名前検索
			if ($type & 0x1) {
				if ($elem[0] =~ s/(\Q$word\E)(?![^<>]*>)/<span class="res">$1<\/span>/g) {
					$bFind = 1;
				}
			}
			# 本文検索
			if ($type & 0x2) {
				if ($elem[3] =~ s/(\Q$word\E)(?![^<>]*>)/<span class="res">$1<\/span>/g) {
					$bFind = 1;
				}
			}
			# ID or 日付検索
			if ($type & 0x4) {
				if ($elem[2] =~ s/(\Q$word\E)(?![^<>]*>)/<span class="res">$1<\/span>/g) {
					$bFind = 1;
				}
			}
			if ($bFind) {
				my $SetStr = "$bbs<>$key<>" . ($i + 1) . '<>';
				$SetStr .= join('<>', @elem);
				push @$pResultSet, $SetStr;
			}
		}
	}
	$ARAGORN->Close();
}

#============================================================================================================
#	Module END
#============================================================================================================
1;
