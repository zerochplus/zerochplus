#============================================================================================================
#
#	出力管理モジュール
#
#============================================================================================================
package	THORIN;

use strict;
use warnings;

use Template;

#------------------------------------------------------------------------------------------------------------
#
#	モジュールコンストラクタ - new
#	-------------------------------------------
#	引　数：なし
#	戻り値：モジュールオブジェクト
#
#------------------------------------------------------------------------------------------------------------
sub new
{
	my $class = shift;
	
	my $obj = {
		'BUFF'	=> [],
		'TT'	=> undef,
		'TMPL'	=> undef,
		'DATA'	=> undef,
	};
	bless $obj, $class;
	
	return $obj;
}

sub Init
{
	my $this = shift;
	my ($tmpl) = @_;
	
	my $template = Template->new({
		'RELATIVE'		=> 1,
		'INCLUDE_PATH'	=> './template',
		'COMPILE_DIR'	=> './template/compiled',
	});
	
	$this->{'TT'} = $template;
	$this->{'TMPL'} = $tmpl;
	$this->{'DATA'} = {};
}

sub Set
{
	my $this = shift;
	my ($data) = @_;
	
	my $d = $this->{'DATA'};
	$d->{$_} = $data->{$_} foreach (keys %$data);
}

sub Output
{
	my $this = shift;
	my ($ref) = @_;
	
	return if (!defined $this->{'TT'});
	
	$this->{'TT'}->process($this->{'TMPL'}, $this->{'DATA'}, $ref)
											or warn $this->{'TT'}->error;
}

sub OutputContentType
{
	my $this = shift;
	my ($type, $ref) = @_;
	
	if (ref($ref) eq 'SCALAR') {
		$$ref .= "Content-Type: $type\n\n";
	}
	elsif (ref($ref) eq 'GLOB' && ref(*$ref{IO}) eq 'IO::File') {
		print $ref "Content-Type: $type\n\n";
	}
	else {
		print "Content-Type: $type\n\n";
	}
}

#------------------------------------------------------------------------------------------------------------
#
#	バッファ出力 - Print
#	-------------------------------------------
#	引　数：$line : 出力テキスト
#	戻り値：なし
#
#------------------------------------------------------------------------------------------------------------
sub Print
{
	my $this = shift;
	my ($line) = @_;
	
	push @{$this->{'BUFF'}}, $line;
}

#------------------------------------------------------------------------------------------------------------
#
#	INPUTタグ出力 - HTMLInput
#	-------------------------------------------
#	引　数：$kind  : タイプ
#			$name  : 名前
#			$value : 値
#	戻り値：なし
#
#------------------------------------------------------------------------------------------------------------
sub HTMLInput
{
	my $this = shift;
	my ($kind, $name, $value) = @_;
	
	my $line = "<input type=$kind name=\"$name\" value=\"$value\">\n";
	
	push @{$this->{'BUFF'}}, $line;
}

#------------------------------------------------------------------------------------------------------------
#
#	バッファフラッシュ - Flush
#	-------------------------------------------
#	引　数：$flag       : 出力フラグ
#			$perm		: パーミッション
#			$szFilePath : 出力パス
#	戻り値：なし
#
#------------------------------------------------------------------------------------------------------------
sub Flush
{
	my $this = shift;
	my ($flag, $perm, $ref) = @_;
	
	# ファイルへ出力
	if ($flag) {
		if (open(my $fh, (-f $ref ? '+<' : '>'), $ref)) {
			flock($fh, 2);
			seek($fh, 0, 0);
			print $fh @{$this->{'BUFF'}};
			truncate($fh, tell($fh));
			close($fh);
		}
		chmod $perm, $ref;
	}
	elsif (ref($ref) eq 'SCALAR') {
		$$ref = join ('', @{$this->{'BUFF'}});
	}
	elsif (ref($ref) eq 'GLOB' && ref(*$ref{IO}) eq 'IO::File') {
		print $ref @{$this->{'BUFF'}};
	}
	# 標準出力に出力
	else {
		print @{$this->{'BUFF'}};
	}
}

#------------------------------------------------------------------------------------------------------------
#
#	バッファクリア - Clear
#	-------------------------------------------
#	引　数：なし
#	戻り値：なし
#
#------------------------------------------------------------------------------------------------------------
sub Clear
{
	my $this = shift;
	
	$this->{'BUFF'} = [];
}

#------------------------------------------------------------------------------------------------------------
#
#	マージ - Merge
#	-------------------------------------------
#	引　数：$thorin : THORINモジュール
#	戻り値：なし
#
#------------------------------------------------------------------------------------------------------------
sub Merge
{
	my $this = shift;
	my ($thorin) = @_;
	
	push @{$this->{'BUFF'}}, @{$thorin->{'BUFF'}};
}

#============================================================================================================
#	モジュール終端
#============================================================================================================
1;
